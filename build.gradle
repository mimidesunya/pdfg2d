plugins {
    id 'java-library'
    id 'maven-publish'
    id 'signing'
}

allprojects {
    group = 'io.github.mimidesunya'
    version = '1.2.0'
    
    repositories {
        mavenCentral()
    }
}

subprojects {
    apply plugin: 'java-library'
    apply plugin: 'maven-publish'
    apply plugin: 'signing'
    
    java {
        sourceCompatibility = JavaVersion.VERSION_21
        targetCompatibility = JavaVersion.VERSION_21
        withSourcesJar()
        withJavadocJar()
    }
    
    dependencies {
        testImplementation 'org.junit.jupiter:junit-jupiter:5.7.1'
        testRuntimeOnly 'org.junit.platform:junit-platform-launcher'
    }

    test {
        useJUnitPlatform()
    }
    
    compileJava {
        options.encoding = 'UTF-8'
    }
    
    compileTestJava {
        options.encoding = 'UTF-8'
    }
    
    javadoc {
        options.encoding = 'UTF-8'
        options.addStringOption('Xdoclint:none')
        if(JavaVersion.current().isJava9Compatible()) {
            options.addBooleanOption('html5', true)
        }
        destinationDir = file("${buildDir}/docs/javadoc_custom")
        enabled = false
    }
    
    publishing {
        publications {
            maven(MavenPublication) {
                from components.java
                
                pom {
                    name = "${project.group}:${project.name}"
                    url = "https://github.com/mimidesunya/pdfg2d/"
                    
                    licenses {
                        license {
                            name = 'Apache 2'
                            url = 'http://www.apache.org/licenses/LICENSE-2.0.txt'
                            distribution = 'repo'
                            comments = 'A business-friendly OSS license'
                        }
                    }
                    
                    developers {
                        developer {
                            id = 'mimidesunya'
                            name = 'MIYABE Tatsuhiko'
                            email = 'miyabe.tatsuhiko@gmail.com'
                            organization = 'Zamasoft'
                            organizationUrl = 'https://zamasoft.net/'
                            roles = ['architect', 'developer']
                            timezone = '+9'
                        }
                    }
                    
                    scm {
                        connection = 'scm:git:https://github.com/mimidesunya/pdfg2d.git'
                        developerConnection = 'scm:git:https://github.com/mimidesunya/pdfg2d.git'
                        url = 'https://github.com/mimidesunya/pdfg2d'
                    }
                }
            }
        }
        
        repositories {
            maven {
                name = "OSSRH"
                url = version.endsWith('SNAPSHOT') ? 
                    "https://s01.oss.sonatype.org/content/repositories/snapshots/" :
                    "https://s01.oss.sonatype.org/service/local/staging/deploy/maven2/"
                credentials {
                    username = project.findProperty("ossrhUsername") ?: ""
                    password = project.findProperty("ossrhPassword") ?: ""
                }
            }
        }
    }
    
    signing {
        sign publishing.publications.maven
    }
}

description = 'Mapping Java Graphics2D to PDF'

// Skip pdfg2d-svg-emoji from default build to speed up builds
// To build with emoji fonts: ./gradlew build -PincludeEmojiFonts=true
// Or build just the fonts module: ./gradlew :pdfg2d-svg-emoji:build
gradle.taskGraph.whenReady { graph ->
    if (!project.hasProperty('includeEmojiFonts')) {
        project(':pdfg2d-svg-emoji').tasks.each { task ->
            if (task.name in ['jar', 'processResources', 'classes', 'compileJava', 
                              'sourcesJar', 'javadocJar', 'javadoc', 'build', 'assemble']) {
                task.enabled = false
            }
        }
    }
}
