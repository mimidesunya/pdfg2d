description = 'SVG and Emoji support'

dependencies {
    implementation project(':pdfg2d-pdf')
    implementation project(':pdfg2d-core')
    implementation project(':pdfg2d-font')
    implementation 'org.apache.xmlgraphics:batik-bridge:1.16'
    implementation 'org.apache.xmlgraphics:batik-anim:1.16'
    implementation 'xml-apis:xml-apis:1.4.01'
    implementation 'fr.sertelon.media:jpeg-cmyk:1.1.0'
}

publishing {
    publications {
        maven(MavenPublication) {
            pom {
                description = 'SVG and Emoji support'
                url = 'https://github.com/mimidesunya/pdfg2d/tree/main/pdfg2d-svg-emoji'
                
                scm {
                    connection = 'scm:git:https://github.com/mimidesunya/pdfg2d/tree/main/pdfg2d-svg-emoji'
                    developerConnection = 'scm:git:https://github.com/mimidesunya/pdfg2d/tree/main/pdfg2d-svg-emoji'
                    url = 'https://github.com/mimidesunya/pdfg2d/tree/main/pdfg2d-svg-emoji'
                }
            }
        }
    }
}

task generateEmojiIndex {
    def emojiDir = file("src/main/resources/net/zamasoft/pdfg2d/font/emoji")
    def indexFile = new File(emojiDir, "INDEX")
    
    inputs.dir emojiDir
    outputs.file indexFile

    doLast {
        indexFile.withPrintWriter("ISO-8859-1") { out ->
            Set<String> codes = new HashSet<>()
            emojiDir.eachFile { file ->
                if (file.name.endsWith(".svg")) {
                    String code = file.name.substring(7, file.name.length() - 4)
                    while (true) {
                        if (!codes.contains(code)) {
                            out.println(code)
                            codes.add(code)
                        }
                        int ub = code.lastIndexOf('_')
                        if (ub == -1) {
                            break
                        }
                        code = code.substring(0, ub)
                    }
                }
            }
            out.println("200d")
            
            emojiDir.eachFile { file ->
                if (file.name.endsWith(".svg")) {
                    String code = file.name.substring(7, file.name.length() - 4)
                     code.split("_").each { c ->
                        if (!codes.contains(c)) {
                             codes.add(c)
                        }
                    }
                }
            }
        }
    }
}

processResources.dependsOn generateEmojiIndex
